
rye .needs { fyne }

do\in fyne {

	app .window "Tabs" :win
	
    tab1: vbox [
        label "Tab 1"
        hbox [
            label "Button status: "
            button-status: label "Unclicked"
        ]
        button "Click me" { button-status .set-text "Clicked" }
    ]

    prog: progressbar
    tab2: vbox [
        label "Tab 2"
        btn: button "Add progress bar" {
			tab2 .add prog , btn .disable
			go does { for range 1 20 { * 0.05 \set-value* prog , sleep 500 } }
		}
    ]

    tab3: vbox [
        label "Tab 3"
        selectbox [ "First" "Second" "Third" ] :ent-select
        hbox [
            label "Select status: "
            select-status: label "Unselected"
        ]
        button "Check select" { text: ent-select .get-text , select-status .set-text text }
    ]

    content: vbox [
        label "Select a tab to view its content"
    ]

    button-row: hbox [
        button "Tab 1" { content .remove-all , content .add tab1 }
        button "Tab 2" { content .remove-all , content .add tab2 }
        button "Tab 3" { content .remove-all , content .add tab3 }
    ]

    main: vbox [
        button-row
        content
    ]

	win .set-content main \show-and-run

}

rye .needs { fynegen }

do\in fynegen {
	win: app
	|window "Rye Fyne Demo"
	|resize size 400.0 400.0

	win
	|set-content app-tabs [
	   tab-item "Basic" v-scroll v-box [
			label "This is a simple label\nBottom Text"
			rich-text-from-markdown "
# This is some markdown
## MD List
- Item 1
- Item 2

## Fyne logo
![Fyne Logo](https://fyne.io/img/transparent.png)

## Rye language
[https://ryelang.org/](https://ryelang.org/)
"
			separator
			entry .place-holder! "This is an entry"
			separator
			button-with-icon "Click me" download-icon fn { } {
				show-pop-up label "Button clicked!" win .canvas
			}
			separator
			accordion [
				accordion-item
					"Programming languages"
					rich-text-from-markdown
						"- Rye\n- Go\n- C\n- C++\n- Python\n- Zig"
				accordion-item
					"Operating Sytems"
					rich-text-from-markdown
						"- Android\n- Linux\n- FreeBSD\n- MacOS\n- Windows"
			]
			separator
			center [ h-box [
				icon theme-file-icon
				icon document-save-icon
				icon document-create-icon
				icon delete-icon
				icon content-undo-icon
				icon content-redo-icon
				file-icon storage-uri "file:///nonexistant/path/image.png"
			] ]
			separator
			check "Check me" closure { ok } { }
			separator
			form [
				form-item "First Name"
					entry .place-holder! "John"
				form-item "Last Name"
					entry .place-holder! "Doe"
				form-item "Gender" do {
					select-entry [ "Male" "Female" ]
					|place-holder! "(Select one or type in entry)"
				}
				form-item "E-mail" do {
					entry
					|place-holder! "john@doe.com"
					|validator! fn { s } {
						re: regexp "^[\w-\.]+@([\w-]+\.)+[\w-]{2,99}$" ; don't use regexp for email validation, this is for demo purposes only
						either re .is-match s {
							nil
						} {
							"Invalid e-mail"
						}
					}
				}
			]
			separator
			progress-bar .value! 0.69 :progbar
			progress-bar-infinite
			separator
			do {
				slider 0.0 100.0
				|value! 69.0
				|on-changed! fn { x } { progbar .set-value x / 100.0 }
			}
		]
	]
	|show-and-run
}

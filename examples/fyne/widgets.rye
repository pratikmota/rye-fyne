rye .needs { fyne }

do\in fyne {
	win: app
	|window "Rye Fyne Gallery"
	|resize size 500.0 500.0

	win
	|set-content app-tabs [
	   tab-item "Display" v-scroll v-box [
			label "This is a simple\nmultiline label"

			separator

			rich-text-from-markdown "
# This is rich-text from markdown
## MD List
- Item 1
- Item 2

## Fyne logo
![Fyne Logo](https://fyne.io/img/transparent.png)

## Rye language
[https://ryelang.org/](https://ryelang.org/)
"
			separator

			label "Icons"
			center [ h-box [
				icon theme-file-icon
				icon document-save-icon
				icon document-create-icon
				icon delete-icon
				icon content-undo-icon
				icon content-redo-icon
				file-icon storage-uri "file:///nonexistant/path/image.png"
			] ]

			separator
			label "Progress bar"			
			progress-bar .value! 0.69
			
			separator
			label "Infinite progress bar"			
			progress-bar-infinite
		]
		
		tab-item "Entry" v-scroll v-box [
			entry .place-holder! "Text entry"
			button "Button" fn { } {
				show-pop-up label "Button clicked!" win .canvas
			}
			separator
			button-with-icon "Button with icon" download-icon fn { } {
				show-pop-up label "Button clicked!" win .canvas
			}
			separator
			check "Checkbox" closure { ok } { }
			separator
			radio-group [ "Radio" "Group" ] closure { ok } { }
			select [ "select 1" "select 2" ] closure { ok } { }
			separator
			select-entry [ "one" "type in" ]
			|place-holder! "(Select one or type in)"

			separator

;			do {
				slider 0.0 100.0
				|value! 69.0
;				|on-changed! fn { x } { progbar .set-value x / 100.0 }
;			}

			separator

			label "A form with email validation"
			
			form [
				form-item "First Name"
					entry .place-holder! "John"
				form-item "Gender" do {
					select-entry [ "Male" "Female" "Other" ]
					|place-holder! "(Select one or type in entry)"
				}
				form-item "E-mail" do {
					entry
					|place-holder! "john@doe.com"
					|validator! fn { s } {
						re: regexp "^[\w-\.]+@([\w-]+\.)+[\w-]{2,99}$" ; don't use regexp for email validation, this is for demo purposes only
						either re .is-match s {
							nil
						} {
							"Invalid e-mail"
						}
					}
				}
			]
		]
		tab-item "Containers" v-scroll v-box [
			h-box [ label "h-box" label "h-box" ]
			separator
			v-box [ label "v-box" label "v-box" ]
			separator
			center [ label "center" ]
			separator
			scroll v-box [ label "scroll" label "scroll" ]
			separator
			accordion [
				accordion-item
					"accordion item 1"
					rich-text-from-markdown
						"- Rye\n- Go\n- C\n- C++\n- Python\n- Zig"
				accordion-item
					"accordion item 2"
					rich-text-from-markdown
						"- Android\n- Linux\n- FreeBSD\n- MacOS\n- Windows"
			]
			separator
			app-tabs [
				tab-item "Tab 1" label "Content 1"
				tab-item "Tab 2" label "Content 2"
				tab-item "Tab 3" label "Content 3"
			]
		]
	]
	|show-and-run
}

; Requires -tags b_fyne

rye .needs { fyne }

do\in fyne {
	a: app .window "Hello"

	lab1: rich-text-from-markdown "## Hello, world!"
	lab2: label "The fyne bindings for this were generated :)"
	lab2 .wrapping! text-wrap-word

	sld1: slider 0.0 10.0
	sld1 .step! 0.5
	sld1 .on-changed! fn { val } {
		lab2 .set-text "Slider value is " + val .to-string + "."
	}

	chk1: check "Check" fn { checked } {
		chk-text: either checked "checked" "not checked"
		lab2 .set-text "Checkbox is " + chk-text + "."
	}

	btn1: button-with-icon "Button" question-icon fn { } {
		lab2 .set-text "Button clicked"
	}

	ent1: entry
	ent1 .on-changed! fn { text } {
		lab2 .set-text
			either text { "Entry text is '" + text + "'." } { "Entry is empty." }
	}
	ent1 .place-holder! "Enter some text..."
	ent1 .multi-line! true
	ent1 .wrapping! text-wrap-word

	a
	|set-content v-box [
		lab1
		separator
		lab2
		sld1
		ent1
		btn1
		chk1
	]
	|show-and-run
}
